#!/usr/bin/env polaris

let List = require("list.pol");


# Actual build logic

let extractTags(file) = {
    let keys = lines(!grep "-Po" "(?<=<!--).+?(?=:)" file);
    let values = lines(!grep "-Po" "(?<=:).+?(?=-->)" file);
    List.zip(keys, values);
};

let replaceTags(tags, content) = {
    List.foldr(\(tag, r) -> replace("{{" ~ List.fst(tag) ~ "}}", List.snd(tag), r), content, tags)
};

let escapeHTML(content) = {
    let toReplace = 
    [ ["<", "&lt;"]
    , [">", "&gt;"]
    , ["<!--.*?-->", ""]
    ];

    List.foldr(\(x, r) -> regexpReplace(List.fst(x), List.snd(x), r), content, toReplace)
};

let static_tags = [["header", !cat "static-html/header.html"]];

let indexTags = [];

let buildPage(path, template, tags) = {
    print("Building page: " ~ path);
    let rendered_page = replaceTags(tags, !cat ("templates/" ~ template));
    
    writeFile(path, rendered_page)
};

let buildPost(name) = {
    print("Building post: " ~ name);
    let markdownPath = "md/" ~ name ~ ".md";
    let htmlPath = "posts/" ~ name ~ ".html";

    let tags = extractTags(markdownPath);

    let htmlContent = replaceTags(tags, !pandoc markdownPath);

    let postTags = tags ~ 
        [ ["header", !cat "static-html/header.html"]
        , ["title-no-code", regexpReplace("<code>(.+?)</code>", "$1", List.lookup("title", tags))]
        , ["body", htmlContent]
        ];

    let renderedPost = replaceTags(postTags, !cat "templates/post.html");

    writeFile(htmlPath, renderedPost);

    let additionalTags = 
        [ ["content", escapeHTML(htmlContent)]
        , ["link", "https://prophetlabs.de/posts/" ~ name ~ ".html"]
        ];

    indexTags := cons(additionalTags ~ postTags, indexTags);
};

let buildIndex(indexTags) = {
    let summaryTemplate = !cat "templates/post-summary.html";
    let summaries = List.foldr(\(tags, r) -> replaceTags(tags, summaryTemplate) ~ r, "", indexTags);
    buildPage("index.html", "index.html", static_tags ~ [["post-summaries", summaries]])
};

let buildRSS(indexTags, pubDate) = {
    let itemTemplate = !cat "templates/rss-item.xml";
    let items = List.foldr(\(tags, r) -> replaceTags(tags, itemTemplate) ~ r, "", indexTags);
    buildPage("rss.xml", "rss.xml", static_tags ~ [["rss-items", items], ["pubDate", pubDate], ["lastBuildDate", pubDate]])
};

let buildScss() = {
    let files = lines(!find "scss/post.scss" "-name" "*.scss");
    List.for(files, \file -> {
        print("Building scss file: " ~ file);
        !sass file ("assets/" ~ (!basename "-s" ".scss" file) ~ ".css")
    })
};

buildPost("unsafeCoerceDict");

buildPage("about.html", "about.html", static_tags);

buildIndex(indexTags);
buildRSS(indexTags, "05-05-2022");

buildScss();
